
set background = "#dfa"
set initial_heading = 90
set line_width = .2

# Seen in [http://piratefsh.github.io/p5js-art/public/lsystems/]:

main lsystem WavySeaweed {

	axiom FF

	set iterations = 4
	set rotation = 22.5
	set line_width = .1

	F -> FF-[-F+F+F]+[+F-F-F]

}

lsystem TallSeaweed {

	axiom F

	set iterations = 4
	set rotation = 25

	F -> F[+F]F[-F]F

}

lsystem FuzzyWeed {

	axiom X

	set iterations = 5
	set rotation = 25

	X -> F+[[X]-X]-F[-FX]+X
	F -> FF

	X => F

}

lsystem ArrowWeed {

	axiom X

	set iterations = 5
	set rotation = 30

	F -> FF
	X -> F[+X][-X]FX

}

lsystem TwiggyWeed {

	axiom X

	set iterations = 5
	set rotation = 25

	F -> FF
	X -> F[-X]F[-X]+X

}

lsystem StochasticFuzzyWeed {

	axiom X

	set iterations = 4
	set rotation = 22.5

	F -> FF
	X -> F-[[X]+X]+F[+FX]-X
	X -> F+[[X]-X]-F[-FX]+X

}

# Seen in [http://paulbourke.net/fractals/lsys/]:

lsystem Leaf1 {

	axiom a

	set iterations = 14
	set rotation = 45
	set line_width = 1

	F(l) -> F(l*1.36)
	F() -> F(1.36)
	a -> F[+x]Fb
	b -> F[-y]Fa
	x -> a
	y -> b

}

lsystem Weed1 {

	axiom Y

	set iterations = 6
	set rotation = 25.7

	X -> X[-FFF][+FFF]FX
	Y -> YFX[+Y][-Y]

}

lsystem Weed2 {

	axiom F

	set iterations = 5
	set rotation = 35

	F -> F[+FF][-FF]F[-F][+F]F

}

lsystem SaupeWeed {

	axiom VZFFF

	set iterations = 10
	set rotation = 20
	set line_width = .1

	V -> [+++W][---W]YV
	W -> +X[-W]Z
	X -> -W[+X]Z
	Y -> YZ
	Z -> [-FFF][+FFF]F

}

lsystem Weed3 {

	axiom aF

	set iterations = 18
	set rotation = 12
	set line_width = .7

	a -> FFFFFv[+++h][---q]fb
	b -> FFFFFv[+++h][---q]fc
	c -> FFFFFv[+++fa]fd
	d -> FFFFFv[+++h][---q]fe
	e -> FFFFFv[+++h][---q]fg
	g -> FFFFFv[---fa]fa
	h -> ifFF
	i -> fFFF[--m]j
	j -> fFFF[--n]k
	k -> fFFF[--o]l
	l -> fFFF[--p]
	m -> fFn
	n -> fFo
	o -> fFp
	p -> fF
	q -> rfF
	r -> fFFF[++m]s
	s -> fFFF[++n]t
	t -> fFFF[++o]u
	u -> fFFF[++p]
	v -> Fv

	!| F => F
	!| f => f
	!| + => +
	!| - => -
	   _ =>

}

lsystem Weed4 {

	axiom aF

	set iterations = 25
	set rotation = 18
	set line_width = 1.5

	a -> FFFFFy[++++n][----t]fb
	b -> +FFFFFy[++++n][----t]fc
	c -> FFFFFy[++++n][----t]fd
	d -> -FFFFFy[++++n][----t]fe
	e -> FFFFFy[++++n][----t]fg
	g -> FFFFFy[+++fa]fh
	h -> FFFFFy[++++n][----t]fi
	i -> +FFFFFy[++++n][----t]fj
	j -> FFFFFy[++++n][----t]fk
	k -> -FFFFFy[++++n][----t]fl
	l -> FFFFFy[++++n][----t]fm
	m -> FFFFFy[---fa]fa
	n -> ofFFF
	o -> fFFFp
	p -> fFFF[-s]q
	q -> fFFF[-s]r
	r -> fFFF[-s]
	s -> fFfF
	t -> ufFFF
	u -> fFFFv
	v -> fFFF[+s]w
	w -> fFFF[+s]x
	x -> fFFF[+s]
	y -> Fy

	!| F => F
	!| f => f
	!| + => +
	!| - => -
	   _ =>

}
